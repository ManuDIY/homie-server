; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Homie Server"
#define MyAppPublisher "Marvin Roger"
#define MyAppURL "https://github.com/marvinroger/homie-server"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C08C90F4-30CF-492B-BE8A-03514A96E845}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
SourceDir={#MySourceDir}
OutputDir="..\output"
OutputBaseFilename={#MyAppOutput}
SetupIconFile="icon.ico"
UninstallDisplayIcon="{app}\icon.ico"
UninstallDisplayName={#MyAppName}
Compression=lzma
SolidCompression=yes

[Tasks]
Name: install_service; Description: "Install as a service";

[Icons]
Name: "{group}\Homie Server"; Filename: "{app}\homie.cmd"; WorkingDir: "{app}"; IconFilename: "{app}\icon.ico"
Name: "{group}\Uninstall"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\nssm.exe"; Parameters: "install ""{#MyAppName}"" ""{app}\homie.cmd"" ""--uiPort 35589 --dataDir {localappdata}\HomieServer"""; StatusMsg: "Installing service..."; Tasks: install_service; Flags: runhidden
Filename: "{app}\nssm.exe"; Parameters: "set ""{#MyAppName}"" AppStdout ""{app}\log.txt"""; StatusMsg: "Setting stdout log file..."; Tasks: install_service; Flags: runhidden
Filename: "{app}\nssm.exe"; Parameters: "set ""{#MyAppName}"" AppStderr ""{app}\log.txt"""; StatusMsg: "Setting stderr log file..."; Tasks: install_service; Flags: runhidden
Filename: "{app}\nssm.exe"; Parameters: "start ""{#MyAppName}"""; StatusMsg: "Starting service..."; Tasks: install_service; Flags: runhidden

[UninstallRun]
Filename: "{app}\nssm.exe"; Parameters: "stop ""{#MyAppName}"""; Tasks: install_service; Flags: runhidden
Filename: "{app}\nssm.exe"; Parameters: "remove ""{#MyAppName}"" confirm"; Tasks: install_service; Flags: runhidden

[UninstallDelete]
Type: files; Name: "{app}\log.txt"; Tasks: install_service
Type: files; Name: "{app}\errlog.txt"; Tasks: install_service
Type: filesandordirs; Name: "{localappdata}\HomieServer"; Tasks: install_service

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "icon.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "bundle\*"; DestDir: "{app}"; Flags: ignoreversion
Source: "bundle\node_modules\*"; DestDir: "{app}\node_modules"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
